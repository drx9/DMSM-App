// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = "35.0.0"
        minSdkVersion = 24
        compileSdkVersion = 35
        targetSdkVersion = 35
        // ndkVersion = "27.1.12297006" // Disabled to avoid NDK issues
        kotlinVersion = "2.0.21"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.1.4")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion")
        classpath("com.google.gms:google-services:4.3.15")
    }
}

def reactNativeAndroidDir = new File(
  providers.exec {
    workingDir(rootDir)
    commandLine("node", "--print", "require.resolve('react-native/package.json')")
  }.standardOutput.asText.get().trim(),
  "../android"
)

allprojects {
  repositories {
    maven {
      // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
      url(reactNativeAndroidDir)
    }

    google()
    mavenCentral()
    maven { url 'https://www.jitpack.io' }
  }
}

// Fix for expo-firebase-core issues
subprojects {
    afterEvaluate { subproject ->
        if (subproject.hasProperty('android')) {
            subproject.android {
                compileSdkVersion rootProject.ext.compileSdkVersion
                buildToolsVersion rootProject.ext.buildToolsVersion
            }
        }
        // Fix for androidSourcesJar classifier issue
        subproject.tasks.withType(org.gradle.api.tasks.bundling.Jar) { task ->
            if (task.name == 'androidSourcesJar') {
                task.archiveClassifier.set('sources')
            }
        }
    }
}

apply plugin: "expo-root-project"
apply plugin: "com.facebook.react.rootproject"
